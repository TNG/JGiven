//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath libs.androidGradlePlugin
    }
}

repositories {
    google()
}

apply plugin: 'com.android.application'

android {
    compileSdk = androidCompileSdkVersion
    namespace = 'jgiven.tngtech.com.jgiven_android.example'
    defaultConfig {
        applicationId = "com.tngtech.jgiven.android.example"
        minSdkVersion = androidMinSdkVersion
        targetSdkVersion = androidTargetSdkVersion
        versionCode = 1
        versionName = "${version}"
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    jgivenReport
}

dependencies {
    implementation(libs.androidx.appcompat) {
        exclude group: 'com.google.guava', module: 'listenablefuture'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }
    androidTestImplementation('androidx.test.espresso:espresso-core:3.6.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.guava', module: 'listenablefuture'
    })
    androidTestImplementation(libs.androidx.test.junit) {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    androidTestImplementation(libs.androidx.test.rules) {
        exclude group: "junit"
    }
    androidTestImplementation(project(':jgiven-android'))
    jgivenReport project(':jgiven-html5-report')
    jgivenReport libs.slf4j.java.util.logging
}

def targetDir = 'build/reports/jgiven/json'
def adb = android.adbExecutable.toString()
def reportsDir = '/storage/emulated/0/Download/jgiven-reports'

task cleanJGivenReports(type: Delete) {
    delete targetDir
}

task pullJGivenReports(type: Exec, dependsOn: cleanJGivenReports) {
    doFirst {
        if (!file(targetDir).mkdirs()) {
            println("Cannot create dir " + targetDir)
        }
    }

    commandLine adb, 'pull', reportsDir, targetDir

    doLast {
        println("Pulled " + reportsDir + " to " + targetDir)
    }
}

task cleanJGivenReportsFromDevice(type: Exec) {
    commandLine adb, 'shell', 'rm -rf', reportsDir

    doLast {
        println("Deleted " + reportsDir)
    }
}

pullJGivenReports.finalizedBy(cleanJGivenReportsFromDevice)

task cleanJGivenHtmlReport(type: Delete) {
    delete 'build/reports/jgiven/html'
}

task jgivenHtml5Report(type: JavaExec, dependsOn: cleanJGivenHtmlReport) {
    //noinspection GroovyAccessibility
    mainClass = 'com.tngtech.jgiven.report.ReportGenerator'
    args '--sourceDir=build/reports/jgiven/json',
            '--targetDir=build/reports/jgiven/html',
            '--format=html5',
            '--exclude-empty-scenarios=true'
    classpath = configurations.jgivenReport
}

pullJGivenReports.finalizedBy(jgivenHtml5Report)

tasks.whenTaskAdded { task ->
    if (task.name == 'connectedAndroidTest') {
        task.finalizedBy 'pullJGivenReports'
    }
}
