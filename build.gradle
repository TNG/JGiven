apply plugin: 'sonar-runner'


buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.kt3k.coveralls'

description = 'JGiven - BDD in plain Java'

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

allprojects {
    group = 'com.tngtech.jgiven'
    version = version
    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
    ext.isCI = System.env.CI == "true"
    apply plugin: 'jacoco'
    repositories {
        mavenCentral()
    }
}


if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'org.asciidoctor.convert'

    description "${rootProject.description} - Module ${project.name}"
    sourceCompatibility = targetCompatibility = 1.6

    ext {
        junitDataproviderVersion = '1.10.2'
        // set default junit version if not set via command line
        junitVersion = rootProject.hasProperty('junitVersion') ? rootProject.junitVersion : '4.12'
        junitParamsVersion = '1.0.4'
        testngVersion = '6.9.10'
        assertjVersion = '1.7.1'
        slf4jVersion = '1.7.13'
        cglibVersion = '2.2.2'
        paranamerVersion = '2.8'
        jansiVersion = '1.11'
        gsonVersion = '2.5'
        guavaVersion = '19.0'
        springVersion = '3.2.4.RELEASE'
        checkstyleVersion = '2.12.1'
        jacocoVersion = '0.7.2.201409121644'
        quickcheckVersion = '0.6'
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

        testCompile group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
        testCompile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
        testCompile group: 'junit', name: 'junit', version: junitVersion
        testCompile group: 'org.assertj', name: 'assertj-core', version: assertjVersion
        testCompile group: 'com.tngtech.java', name: 'junit-dataprovider', version: junitDataproviderVersion
        testCompile group: 'net.java.quickcheck', name: 'quickcheck', version: quickcheckVersion
    }

    tasks.withType(JavaCompile) {
        // needed for DeSzenarioTest.java as it has Umlauts in the code
        options.encoding = 'UTF-8'
    }

    tasks.withType(Jar) {
        def now = new Date()
        manifest = project.manifest().attributes(
                'Built-By': "Gradle ${gradle.gradleVersion}",
                'Build-Date': now.format('yyyy-MM-dd HH:mm:ss.S'), // TODO destroys incremental build feature, but maybe date without time is ok as well?
                'Copyright': "2013-" + now.format('yyyy') + " TNG Technology Consulting GmbH",
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TNG Technology Consulting GmbH',
                'License': 'Apache License v2.0, January 2004',
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Specification-Vendor': 'TNG Technology Consulting GmbH',
        )
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives javadocJar

        archives sourcesJar
    }

    sonarRunner {
        sonarProperties {
            property "sonar.jacoco.reportPath", "${rootProject.projectDir}/build/jacoco/jacocoTest.exec"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
        }
    }

    test {
        systemProperty 'jgiven.report.dir', 'build/reports/jgiven/json'
        systemProperty 'jgiven.report.text', 'false'
        systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'warn'
        jacoco {
            destinationFile = file("${rootProject.projectDir}/build/jacoco/jacocoTest.exec")
            classDumpFile = file("${rootProject.projectDir}/build/jacoco/classpathdumps")
        }
        testLogging {
            showStandardStreams = true
        }
    }

    javadoc {
        exclude '**/impl/**'
    }

    checkstyle {
        configFile = file("${rootProject.projectDir}/develop/checkstyle-rules.xml")
        showViolations = false
        ignoreFailures = true
    }

    task jgivenHtml5Report(type: JavaExec) {
        main = 'com.tngtech.jgiven.report.ReportGenerator'
        args '--sourceDir=build/reports/jgiven/json',
                '--targetDir=build/reports/jgiven/html5',
                '--format=html5',
                '--exclude-empty-scenarios=true',
                '--customcss=build/resources/test/jgiven/custom.css'

        classpath = configurations.testCompile
    }

    task jgivenAsciiDocReport(type: JavaExec) {
        main = 'com.tngtech.jgiven.report.ReportGenerator'
        args '--sourceDir=build/reports/jgiven/json',
                '--targetDir=build/reports/jgiven/asciidoc',
                '--format=asciidoc'

        classpath = configurations.testCompile
    }

    asciidoctor.sourceDir = new File('build/reports/jgiven/asciidoc')
    asciidoctor.outputDir = new File('build/reports/jgiven/htmladoc')
    asciidoctor.attributes toc: ''

    task copyAsciiDoc(type: Copy, dependsOn: jgivenAsciiDocReport) {
        from 'src/asciidoc'
        into 'build/reports/jgiven/asciidoc'
    }

    copyAsciiDoc.finalizedBy(asciidoctor)

    // -- build and publish artifacts -------------------------------------------------------------------------------------

    signing {
        // requires gradle.properties, see http://www.gradle.org/docs/current/userguide/signing_plugin.html
        required {
            isReleaseVersion && gradle.taskGraph.hasTask('uploadArchives')
        }
        sign configurations.archives
    }

    ext {
        sonatypeUsername = (rootProject.hasProperty('sonatypeUsername')) ?
                rootProject.sonatypeUsername :
                "$System.env.SONATYPE_USERNAME"

        sonatypePassword = (rootProject.hasProperty('sonatypePassword')) ?
                rootProject.sonatypePassword :
                "$System.env.SONATYPE_PASSWORD"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { deployment ->
                    signing.signPom(deployment)
                }
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    // username and password from gradle.properties
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                pom {
                    project {
                        name project.name
                        url 'http://jgiven.org'

                        scm {
                            url 'scm:git@github.com:TNG/jgiven.git'
                            connection 'scm:git@github.com:TNG/jgiven.git'
                            developerConnection 'scm:git@github.com:TNG/jgiven.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'janschaefer'
                                name 'Jan SchÃ¤fer'
                            }
                        }
                    }
                    whenConfigured { pom ->
                        pom.dependencies.find { dep -> dep.groupId == 'junit' && dep.artifactId == 'junit' }.with {
                            version = '[4.9,4.12]'
                            scope = 'provided'
                        }
                        pom.dependencies.removeAll(pom.dependencies.findAll { dep -> dep.scope in ['test'] })
                    }
                }
            }
        }
    }

    // to be able use project.description which may be overridden in later executed build.gradle of subproject
    project.afterEvaluate {
        uploadArchives.repositories {
            mavenDeployer.pom.project {
                description = project.description
            }
        }
    }

    uploadArchives.onlyIf {
        !(isCI && isReleaseVersion)
    }

    uploadArchives.doFirst {
        if (!isCI && isReleaseVersion && !JavaVersion.current().isJava8Compatible()) {
            throw new GradleException("Releases have to be done with Java 8");
        }
    }

    javadoc.onlyIf {
        JavaVersion.current().isJava8Compatible()
    }
}

task overallJacocoReport(type: JacocoReport) {
    executionData = files("build/jacoco/jacocoTest.exec")
    sourceDirectories = files("jgiven-core/src/main/java")
    classDirectories = files("jgiven-core/build/classes/main")
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

overallJacocoReport.dependsOn {
    subprojects*.test
}


coveralls.jacocoReportPath = 'build/reports/jacoco/overallJacocoReport/overallJacocoReport.xml'
coveralls.sourceDirs = ["jgiven-core/src/main/java"]
coveralls.saveAsFile = true

