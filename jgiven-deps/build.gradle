apply plugin: 'com.github.johnrengelman.shadow'

description = "JGiven Dependencies"

configurations {
    injar
    libjar
}

def classesJar = files(getClassesJar())

dependencies {
    injar group: 'com.google.guava', name: 'guava', version: guavaVersion
    injar group: 'org.springframework', name: 'spring-core', version: springVersion
    injar group: 'com.google.code.gson', name: 'gson', version: gsonVersion
    injar group: 'com.thoughtworks.paranamer', name: 'paranamer', version: paranamerVersion
    injar group: 'net.bytebuddy', name: 'byte-buddy', version: bytebuddyVersion
    injar group: 'org.fusesource.jansi', name: 'jansi', version: jansiVersion

    libjar 'com.google.code.findbugs:jsr305:3.0.0'
    libjar classesJar

    compile fileTree(dir: 'build/libs', include: 'deps_shrinked.jar')
}


String getClassesJar() {
    def javaHome = System.properties.'java.home'
    def candidates = ["$javaHome/lib/rt.jar", "$javaHome/bin/rt.jar", "$javaHome/../Classes/classes.jar"]
    def found

    candidates.find {
        if (new File(it).exists()) {
            found = it
            return true
        }
        return false;
    }

    if (!found) {
        throw new GradleException('unable to determine java runtime classes it is none of the expected: ' + candidates)
    }

    found
}

task createMinimizedLibs(type: proguard.gradle.ProGuardTask) {
    injars configurations.injar.files
    libraryjars configurations.libjar.files
    outjars file("build/libs/deps_shrinked.jar")
    configuration "guava.proguard"
}

compileJava.dependsOn(createMinimizedLibs)

shadowJar {
    relocate 'org.springframework', 'com.tngtech.jgiven.relocated.springframework'
    relocate 'com.google.common', 'com.tngtech.jgiven.relocated.guava'
    relocate 'com.google.gson', 'com.tngtech.jgiven.relocated.gson'
    relocate 'org.apache.commons.logging', 'com.tngtech.jgiven.relocated.org.apache.commons.logging'
    relocate 'net.bytebuddy', 'com.tngtech.jgiven.relocated.bytebuddy'
    relocate 'org.fusesource.jansi', 'com.tngtech.jgiven.relocated.jansi'
    relocate 'com.thoughtworks.paranamer', 'com.tngtech.jgiven.relocated.paranamer'

    baseName = 'jgiven-deps'
    classifier = null
    version = null
}

artifacts {
    'default'(file: createMinimizedLibs.outputs.files.singleFile, builtBy: createMinimizedLibs)
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.shadow ]
    }
}
