buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath libs.androidGradlePlugin
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
    google()
}

android {
    compileSdkVersion androidCompileSdkVersion
    namespace 'jgiven.tngtech.com.jgiven_android'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 1
        versionName "${version}"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    publishing {
        singleVariant('release'){
            withSourcesJar()
        }
    }
}

configurations.configureEach {
    exclude group: 'xml-apis', module: 'xml-apis-ext'

}
afterEvaluate {project.ext.publishVariant('release')}

dependencies {
    api 'com.google.guava:guava:31.1-jre'
    api("net.bytebuddy:byte-buddy-android:${libs.versions.byteBuddy.version.get()}")
    api junitVariableVersionLibs.junit4
    implementation libs.androidx.appcompat
    implementation(libs.androidx.test.core)
    implementation(libs.androidx.test.rules) {
        exclude group: "junit"
    }
    androidTestImplementation libs.androidx.test.junit
    api project(":jgiven-junit")
}
