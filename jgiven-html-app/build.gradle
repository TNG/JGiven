plugins {
    id('jgiven-publishing')
    id('com.github.node-gradle.node')
    id('jgiven-checkstyle')
    id('jgiven-java')
}

def htmlAppVersion = '1.1.3'

node {
    download = true
}

task npmPack(type: NpmTask) {
    args = ['pack', "jgiven-html-app@${htmlAppVersion}"]

    doLast {
        copy {
            from tarTree(resources.gzip("jgiven-html-app-${htmlAppVersion}.tgz"))
            into layout.buildDirectory
        }
    }
}

task zipAppDir(type: Zip, dependsOn: npmPack) {
    from layout.getBuildDirectory().dir('package/dist')
    archiveFileName = 'app.zip'
    destinationDirectory = layout.getBuildDirectory().dir('resources/main/com/tngtech/jgiven/report/html5')
}

jar.dependsOn zipAppDir
