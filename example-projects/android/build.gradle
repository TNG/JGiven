//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    mavenLocal()
    mavenCentral()
}

android {
    compileSdkVersion 34
    namespace 'jgiven.tngtech.com.jgiven_android.example'
    defaultConfig {
        applicationId "com.tngtech.jgiven.android.example"
        minSdkVersion 29
        targetSdkVersion 32
        versionCode 1
        versionName "${version}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

configurations {
    jgivenReport
}

dependencies {
    implementation('androidx.appcompat:appcompat:1.7.0') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }

    androidTestImplementation('androidx.test.espresso:espresso-core:3.6.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.guava', module: 'listenablefuture'

    })
    androidTestImplementation('androidx.test.ext:junit:1.2.1') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    androidTestImplementation('androidx.test:rules:1.6.1') {
        exclude group: "junit"
    }
    androidTestImplementation('com.tngtech.jgiven:jgiven-android:' + version)

    jgivenReport 'com.tngtech.jgiven:jgiven-html5-report:' + version
    jgivenReport 'org.slf4j:slf4j-simple:2.0.16'
}

def targetDir = 'build/reports/jgiven/json'
def adb = android.getAdbExe().toString()
def reportsDir = '/storage/emulated/0/Download/jgiven-reports'

task cleanJGivenReports(type: Delete) {
    delete targetDir
}

task pullJGivenReports(type: Exec, dependsOn: cleanJGivenReports) {
    doFirst {
        if (!file(targetDir).mkdirs()) {
            println("Cannot create dir " + targetDir)
        }
    }

    commandLine adb, 'pull', reportsDir, targetDir

    doLast {
        println("Pulled " + reportsDir + " to " + targetDir)
    }
}

task cleanJGivenReportsFromDevice(type: Exec) {
    commandLine adb, 'shell', 'rm -rf', reportsDir

    doLast {
        println("Deleted " + reportsDir)
    }
}

pullJGivenReports.finalizedBy(cleanJGivenReportsFromDevice)

task cleanJGivenHtmlReport(type: Delete) {
    delete 'build/reports/jgiven/html'
}

task jgivenHtml5Report(type: JavaExec, dependsOn: cleanJGivenHtmlReport) {
    //noinspection GroovyAccessibility
    mainClass = 'com.tngtech.jgiven.report.ReportGenerator'
    args '--sourceDir=build/reports/jgiven/json',
            '--targetDir=build/reports/jgiven/html',
            '--format=html5',
            '--exclude-empty-scenarios=true'
    classpath = configurations.jgivenReport
}

pullJGivenReports.finalizedBy(jgivenHtml5Report)

tasks.whenTaskAdded { task ->
    if (task.name == 'connectedAndroidTest') {
        task.finalizedBy 'pullJGivenReports'
    }
}
