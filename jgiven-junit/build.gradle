plugins {
    id 'java-library'
}

description = "Module for writing JGiven tests with JUnit"

dependencies {
    api project(':jgiven-core')
    compileOnly "junit:junit:$junitVersion"
    compile project(':jgiven-html5-report')

    testImplementation "junit:junit:$junitVersion"
    testImplementation "pl.pragmatists:JUnitParams:$junitParamsVersion"
    testImplementation "org.mockito:mockito-all:1.10.19"
    testImplementation "org.powermock:powermock-core:1.7.4"
    testImplementation "org.powermock:powermock-api-mockito:1.7.4"
    testImplementation "org.powermock:powermock-module-junit4:1.7.4"
}

test {
    // PowerMock does not work with Java 9+
    if (JavaVersion.current().isJava9Compatible()) {
        test.exclude '**/PowerMockTest*'
    }
}

test.finalizedBy(jgivenHtml5Report)

def generatedSourceDir = "generatedSrc/java"

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", generatedSourceDir]
        }
    }
}

files { file("../jgiven-core/src/main/translations").listFiles() }.collect {
    def pkg=it.name.split("\\.")[0]

    def props = new Properties()
    it.withInputStream { props.load(it) }
    props.pkg=pkg
    def taskName = pkg+'Translation'

    tasks.create(name: taskName, type: Copy) {
        from 'src/main/templates'
        into generatedSourceDir + "/com/tngtech/jgiven/junit/lang/" + pkg
        rename("SimpleScenarioTest.template",props.simple_scenario_test_class+".java")
        rename("ScenarioTest.template",props.scenario_test_class+".java")
        expand(props)
        filteringCharset = 'UTF-8'
    }
    compileJava.dependsOn += taskName
}
