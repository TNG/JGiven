plugins {
    id 'java-library'
}

description = "Module for writing JGiven tests with JUnit"

dependencies {
    api project(':jgiven-core')
    compileOnly junitVariableVersionLibs.junit4
    implementation project(':jgiven-html5-report')

    testImplementation libs.bundles.junit4
    testImplementation "com.googlecode.junit-toolbox:junit-toolbox:2.4"
    testImplementation "org.mockito:mockito-core:5.10.0"
}

test.finalizedBy(jgivenHtml5Report)

def generatedSourceDir = "generatedSrc/java"

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", generatedSourceDir]
        }
    }
}

files { file("../jgiven-core/src/main/translations").listFiles() }.collect {
    def pkg=it.name.split("\\.")[0]

    def props = new Properties()
    it.withInputStream { props.load(it) }
    props.pkg=pkg
    def taskName = pkg+'Translation'

    tasks.create(name: taskName, type: Copy) {
        from 'src/main/templates'
        into generatedSourceDir + "/com/tngtech/jgiven/junit/lang/" + pkg
        rename("SimpleScenarioTest.template",props.simple_scenario_test_class+".java")
        rename("ScenarioTest.template",props.scenario_test_class+".java")
        expand(props)
        filteringCharset = 'UTF-8'
    }
    sourcesJar.dependsOn += taskName
    compileJava.dependsOn += taskName
}
