
description = "Module for writing JGiven tests with JUnit"

dependencies {
    compile project(':jgiven-core')
    compileOnly(group: 'junit', name: 'junit', version: junitVersion)
    testCompile project(':jgiven-html5-report')
    testCompile(group: 'junit', name: 'junit', version: junitVersion)
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: junitParamsVersion
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'org.powermock', name: 'powermock-core', version: '1.7.4'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.7.4'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.4'
}



test {
    // PowerMock does not work with Java 9+
    if (JavaVersion.current().isJava9Compatible()) {
        test.exclude '**/PowerMockTest*'
    }
}

test.finalizedBy(jgivenHtml5Report)

def generatedSourceDir = "generatedSrc/java"

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", generatedSourceDir]
        }
    }
}

files { file("../jgiven-core/src/main/translations").listFiles() }.collect {
    def pkg=it.getName().split("\\.")[0]

    def props = new Properties()
    it.withInputStream { props.load(it) }
    props.pkg=pkg
    def taskName = pkg+'Translation'

    tasks.create(name: taskName, type: Copy) {
        from 'src/main/templates'
        into generatedSourceDir + "/com/tngtech/jgiven/junit/lang/" + pkg
        rename("SimpleScenarioTest.template",props.simple_scenario_test_class+".java")
        rename("ScenarioTest.template",props.scenario_test_class+".java")
        expand(props)
        filteringCharset = 'UTF-8'
    }
    compileJava.dependsOn += taskName
}
