description = 'JGiven Maven Mojo'


dependencies {
    implementation project(':jgiven-core')
    implementation project(':jgiven-html5-report')
    implementation "org.apache.maven.plugin-tools:maven-plugin-tools-api:3.6.1"
    implementation "org.apache.maven.plugin-tools:maven-plugin-annotations:3.6.1"
}

task copyClasses(type: Copy, dependsOn: compileJava) {
    from 'build/classes/java/main'
    into 'build/maven/target/classes'
}

task generatePom(type: Copy, dependsOn: copyClasses) {
    from 'src/main/maven'
    into 'build/maven'
    filesMatching("**/pom.*") {
        expand(version: project.version)
    }
}

task generateMavenPlugin(type: CrossPlatformExec, dependsOn: generatePom) {
    // currenlty it seems to be the more or less only clean solution
    // to generate a plugin.xml file to use maven directly
    // if anyone has a better solution please let us know!
    buildCommand 'mvn', '-nsu', '-U', '-f', 'build/maven/pom.xml', 'plugin:descriptor'
}

generateMavenPlugin.onlyIf {
   // as the generateMavenPlugin task requires mvn, it is only executed
   // when actually uploading the archives
   // that way the standard build stays maven-free
   gradle.taskGraph.getAllTasks().any{it instanceof AbstractPublishToMaven}
}

task copyMavenPlugin(type: Copy, dependsOn: generateMavenPlugin) {
    from('build/maven/target/classes') {
       include '**/*.xml'
    }
    into 'build/classes/java/main'
}

task copyPom(type: Copy, dependsOn: generatePom) {
    from('build/maven') {
       include '**/pom.*'
       filter { line ->
           line.replaceAll("<version>.*</version>","")
       }
    }
    into 'build/classes/main/META-INF/maven/com.tngtech.jgiven/jgiven-maven-plugin'
    includeEmptyDirs = false

}

jar.dependsOn copyMavenPlugin, copyPom

class CrossPlatformExec extends Exec {
    void buildCommand(String command, String... commandArgs) {
        if(org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            executable = 'cmd'
            args = ['/c', command]
        } else {
            executable = command
        }
        args(commandArgs.toList())
    }
}
