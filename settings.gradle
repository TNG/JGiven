pluginManagement {
    plugins {
        id 'com.tngtech.jgiven.gradle-plugin' version "${version}"

    }

    repositories {
        gradlePluginPortal()
        google()
        maven {
           url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
           url "https://oss.sonatype.org/content/repositories/staging/"
        }
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.7.2'
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

rootProject.name = 'jgiven'

include ':jgiven-core',
        ':jgiven-maven-plugin',
        ':jgiven-junit',
        ':jgiven-spock',
        ':jgiven-testng',
        ':jgiven-spring',
        ':jgiven-spring-junit4',
        ':jgiven-html-app',
        ':jgiven-html5-report',
        ':jgiven-tests',
        ':jgiven-examples',
        ':jgiven-java8-tests',
        ':jgiven-junit5',
        ':jgiven-spring-junit5',
        ':jgiven-gradle-plugin',
        ':reportportal-jgiven-example-tests'

def timingEnabled = System.getenv("JGivenTiming")

if (timingEnabled) {
    include ':jgiven-timing'
}

def release = System.env.RELEASE == "true"
def android = System.env.ANDROID == "true"
boolean hasAndroidSourcePath = ( System.env.ANDROID_SDK_ROOT )

if (JavaVersion.current().isJava9Compatible()) {
    include ':jgiven-java9-tests'
}

if ((android || release) && hasAndroidSourcePath) {
    include ':jgiven-android'
}

if (android && !release && hasAndroidSourcePath) {
    include ':jgiven-android-test'
}
