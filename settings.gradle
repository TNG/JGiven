pluginManagement {
    plugins {
        id 'com.tngtech.jgiven.gradle-plugin' version '1.0.0-RC4'

    }

    repositories {
        gradlePluginPortal()
        jcenter()
        google()
        maven {
            name "JCenter Gradle Plugins"
            url "https://dl.bintray.com/gradle/gradle-plugins"
        }
        maven {
           url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
           url "https://oss.sonatype.org/content/repositories/staging/"
        }
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.0'
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

rootProject.name = 'jgiven'

include ':jgiven-core',
        ':jgiven-maven-plugin',
        ':jgiven-junit',
        ':jgiven-spock',
        ':jgiven-testng',
        ':jgiven-spring',
        ':jgiven-spring-junit4',
        ':jgiven-html-app',
        ':jgiven-html5-report',
        ':jgiven-tests',
        ':jgiven-examples',
        ':jgiven-java8-tests',
        ':jgiven-junit5',
        ':jgiven-spring-junit5'

def release = System.env.RELEASE == "true"
def android = System.env.ANDROID == "true"

if (JavaVersion.current().isJava9Compatible()) {
    include ':jgiven-java9-tests'
}

if (!JavaVersion.current().isJava11Compatible()) {
    include ':jgiven-gradle-plugin'
}

if (android || release) {
    include ':jgiven-android'
}

if (android && !release) {
    include ':jgiven-android-test'
}

